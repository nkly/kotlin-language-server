allprojects { project ->
    task kotlinLSPProjectDeps { task ->
        doLast {
            System.out.println ""
            System.out.println "gradle-version $gradleVersion"
            System.out.println "kotlin-lsp-project ${project.name}"

            if (project.hasProperty('android')) {
                project.android.getBootClasspath().each {
                    System.out.println "kotlin-lsp-gradle $it"
                }

                def variants = []

                if (project.android.hasProperty('applicationVariants')) {
                    variants += project.android.applicationVariants
                }

                if (project.android.hasProperty('libraryVariants')) {
                    variants += project.android.libraryVariants
                }

                variants.each { variant ->
                    def variantBase = variant.baseName.replaceAll("-", File.separator)

                    def buildClasses = project.getBuildDir().absolutePath +
                        File.separator + "intermediates" +
                        File.separator + variantBase +
                        File.separator + "classes"

                    System.out.println "kotlin-lsp-gradle $buildClasses"

                    def userClasses = project.getBuildDir().absolutePath +
                        File.separator + "intermediates" +
                        File.separator + "javac" +
                        File.separator + variantBase +
                        File.separator + "compile" + variantBase.capitalize() + "JavaWithJavac" + File.separator + "classes"

                    System.out.println "kotlin-lsp-gradle $userClasses"

                    def userVariantClasses = project.getBuildDir().absolutePath +
                        File.separator + "intermediates" +
                        File.separator + "javac" +
                        File.separator + variantBase +
                        File.separator + "classes"

                    System.out.println "kotlin-lsp-gradle $userVariantClasses"

                    variant.getCompileClasspath().each {
                        System.out.println "kotlin-lsp-gradle $it"
                    }
                }
            } else if (project.hasProperty('sourceSets')) {
                // Collect all third-party dependencies
                def allDeps = [:]
                def allBins = [] as Set
                configurations.forEach {
                    if (it.canBeResolved) {
                        it.incoming.artifacts.artifacts.forEach {
                            allDeps[it.id.componentIdentifier] = it.file
                            allBins = allBins + it.file
                        }
                    }
                }

                // Print inter-project dependencies, but not third-party ones yet
                sourceSets.forEach {
                    it.compileClasspath.forEach {
                        if (!allBins.contains(it)) {
                            System.out.println "kotlin-lsp-gradle $it"
                        }
                    }
                }

                // Resolve sources for third-party dependencies
                def result = dependencies
                    .createArtifactResolutionQuery()
                    .forComponents(allDeps.keySet())
                    .withArtifacts(JvmLibrary, SourcesArtifact)
                    .execute()

                // Print third-party dependencies with sources
                for (component in result.resolvedComponents) {
                    def bin = allDeps[component.id]
                    def srcs = component.getArtifacts(SourcesArtifact).collect { it.file }
                    if (!srcs.isEmpty()) {
                        def src = srcs.head()
                        System.out.println "kotlin-lsp-gradle-src $bin|$src"
                    } else {
                        System.out.println "kotlin-lsp-gradle $bin"
                    }
                }
            }


            // handle kotlin multiplatform style dependencies if any
            def kotlinExtension = project.extensions.findByName("kotlin")
            if(kotlinExtension && kotlinExtension.hasProperty("targets")) {
                def kotlinSourceSets = kotlinExtension.sourceSets

                // Print the list of all dependencies jar files.
                kotlinExtension.targets.names.each {
                    def classpath = configurations["${it}CompileClasspath"]
                    classpath.files.each {
                        System.out.println "kotlin-lsp-gradle $it"
                    }
                }
            }
        }
    }

    task kotlinLSPAllGradleDeps {
        doLast {
            fileTree("$gradle.gradleHomeDir/lib")
                .findAll { it.toString().endsWith '.jar' }
                .forEach { System.out.println "kotlin-lsp-gradle $it" }
        }
    }
}
